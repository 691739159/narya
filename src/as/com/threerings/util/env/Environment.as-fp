//
// $Id: $
//
// Narya library - tools for developing networked games
// Copyright (C) 2002-2007 Three Rings Design, Inc., All Rights Reserved
// http://www.threerings.net/code/narya/
//
// This library is free software; you can redistribute it and/or modify it
// under the terms of the GNU Lesser General Public License as published
// by the Free Software Foundation; either version 2.1 of the License, or
// (at your option) any later version.
//
// This library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
// Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public
// License along with this library; if not, write to the Free Software
// Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA

package com.threerings.util.env {

import flash.utils.describeType;

import com.threerings.util.ClassUtil;

/**
 * Flash Player specific implementation of Environment.
 */
public class Environment
{
    /**
     * Return an array containing all the public field names of the object
     */
    public static function enumerateFields (obj :Object) :Array
    {
        var result :Array = [ ];
        for each (var bit :String in describeType(obj)..variable.@name) {
            result.push(bit);
        }
        return result;
    }

    /**
     * Returns true if an object of type srcClass is a subclass of or
     * implements the interface represented by the asClass paramter.
     *
     * <code>
     * if (ClassUtil.isAssignableAs(Streamable, someClass)) {
     *     var s :Streamable = (new someClass() as Streamable);
     * </code>
     */
    public static function isAssignableAs (
            asClass :Class, srcClass :Class) :Boolean
    {
        if (asClass == srcClass) {
            return true;

        // if not the same class and srcClass is Object, we're done
        } else if (srcClass == Object) {
            return false;
        }

        // ok, let's introspect on the class and see what we've got.
        var typeInfo :XMLList = describeType(srcClass).child("factory");

        // See which classes we extend.
        var exts :XMLList = typeInfo.child("extendsClass").attribute("type");
        var type :String;
        for each (type in exts) {
            if (asClass == ClassUtil.getClassByName(type)) {
                return true;
            }
        }

        // See which interfaces we implement.
        var imps :XMLList = typeInfo.child("implementsInterface")
            .attribute("type");
        for each (type in imps) {
            if (asClass == ClassUtil.getClassByName(type)) {
                return true;
            }
        }

        return false;
    }
}
}
